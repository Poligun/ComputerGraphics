<script src="script/lib1.js"></script>

<canvas id='canvas1' width="640" height="640"></canvas>

<script id='my_vertex_shader' type='x-shader/x-vertex'>
   attribute vec3 aPosition;
   varying   vec3 vPosition;
   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;
   }
</script>

<script id='my_fragment_shader' type='x-shader/x-fragment'>
    #define pi 3.1415926535

    precision mediump float;
    uniform float uTime;
    uniform vec3  uCursor;
    varying vec3  vPosition;

    vec4 sphere[3];
    vec3 material[3];

    vec3 Lrgb;
    vec3 Ldir;

    vec3 pointLightLoc;
    vec3 pointLightColor;

    // Compute intersection of a ray with a sphere, if any.  Return t.
    // If there is no intersection, return 10000.

    float raySphere(vec3 V, vec3 W, vec4 S)
    {
        vec3 U = V - S.xyz;
        float a = 1.0;
        float b = 2.0 * dot(W, U);
        float c = dot(U, U) - pow(S.w, 2.0);
        float delta = pow(b, 2.0) - 4.0 * a * c;

        if (delta >= 0.0)
            return (-b - sqrt(delta)) / (2.0 * a);
        else
            return 10000.0;
    }

    vec3 procedualTextureColor(vec3 point, vec4 sphere)
    {
        vec3 normal = normalize(point - sphere.xyz);
        vec3 material = vec3(0.5 * abs(sin(normal.x * 2.0)), 0.4 * abs(cos(normal.y * 2.0)), 0.3);
        return material;
    }

    // Diffusely shade a sphere.
    //    point is the x,y,z position of the surface point.
    //    sphere is the x,y,z,r definition of the sphere.
    //    material is the r,g,b color of the sphere.

    vec3 shadeSphere(vec3 point, vec4 sphere, vec3 material)
    {

        // YOU NEED TO COMPUTE COLOR FOR A DIFFUSELY SHADED SPHERE.

        // FOR THE AMBIENT COMPONENT OF THE DIFFUSE SHADING, YOU CAN
        // USE A SIMPLE APPROXIMATION SUCH AS: ambient = material / 5.;
        vec3 normal = normalize(point - sphere.xyz);
        vec3 ambient = 0.05 * material;
        vec3 color = ambient + Lrgb * material * max(0.0, dot(normal, Ldir));

        color += pointLightColor * material * max(0.0, dot(normal, normalize(pointLightLoc - point)));

        return color;
    }

    void main(void)
    {
        Lrgb = vec3(1.0, 0.5, 0.0);
        Ldir = normalize(vec3(uCursor.x, uCursor.y, 1.0 - 2.0 * dot(uCursor.xy, uCursor.xy)));

        pointLightLoc = vec3(-0.5, -0.5, 0.6);
        pointLightColor = 0.7 * vec3(1.0, 1.0, 1.0);

        vec3 V = vec3(0.0, 0.0, -1.0);
        vec3 W = normalize(vec3(vPosition.x, vPosition.y, 0) - V);

        // YOU NEED TO SET x,y,z AND r FOR sphere.
        sphere[0] = vec4(0.0, 0.0, 2.0, 0.7);

        float theta = uTime / 3.0 * pi;
        float phi = pi / 4.0;
        sphere[1] = vec4(cos(theta) * sin(phi), cos(theta) * cos(phi), 2.0 + sin(theta), 0.3);

        sphere[2] = vec4(sin(theta) * cos(phi), sin(theta) * sin(phi), 2.0 + cos(theta), 0.4);

        // YOU NEED TO SET r,g,b FOR material.
        material[0] = vec3(0.8, 0.2, 0.2);
        material[1] = vec3(0.3, 0.3, 0.9);
        material[2] = vec3(0.4, 0.4, 0.4);

        float minT = 10000.0;
        vec4 nearestSphere;
        vec3 sphereMaterial;

        for (int i = 0; i < 3; i++) {
            float t = raySphere(V, W, sphere[i]);
            if (t < minT) {
                minT = t;
                nearestSphere = sphere[i];
                if (i == 0)
                    sphereMaterial = procedualTextureColor(V + minT * W, nearestSphere);
                else
                    sphereMaterial = material[i];
            }
        }

        vec3 color = vec3(0.0, 0.0, 0.0);

        if (minT < 10000.0)
            color = shadeSphere(V + minT * W, nearestSphere, sphereMaterial);

        gl_FragColor = vec4(sqrt(color), 1.0); // Do Gamma correction.
    }
</script>

<script>
start_gl('canvas1', document.getElementById('my_vertex_shader'  ).innerHTML,
                    document.getElementById('my_fragment_shader').innerHTML);
</script>

