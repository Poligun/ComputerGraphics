<canvas id="mainCanvas" width="600px" height="600px"></canvas>

<script id='my_vertex_shader' type='x-shader/x-vertex'>
    attribute vec3 aPosition;
    varying   vec3 vPosition;
    void main()
    {
        gl_Position = vec4(aPosition, 1.0);
        vPosition = aPosition;
    }
</script>

<script src="script/lib1.js"></script>

<script id="my_fragment_shader" type="x-shader/x-fragment">

    #define INF 100000.0
    #define PI 3.1415926535
    #define FOCAL_LENGTH 4.0
    #define BACKGROUND_COLOR vec4(0.0, 0.0, 0.0, 1.0)
    #define BLINN_COEF 40.0
    #define PHONG_COEF 15.0

    precision mediump float;

    uniform float uTime;
    uniform vec3 uCursor;
    uniform bool uPhong;
    varying vec3 vPosition;

    vec3 FocusPosition;

    struct Material
    {
        bool noise;
        vec3 ambient;
        vec3 diffuse;
        vec3 specular;
        float reflectionCoeff;
        float refractionCoeff;
    };

    struct Sphere
    {
        vec3 location;
        float radius;
        Material material;
    };

    struct Light
    {
        vec3 direction;
        vec3 color;
    };

    #define NUMBER_OF_LIGHTS 2
    Light Lights[NUMBER_OF_LIGHTS];

    #define NUMBER_OF_SPHERES 2
    Sphere Spheres[NUMBER_OF_SPHERES];

    vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
    vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
    vec4 permute(vec4 x) { return mod289(((x*34.0)+1.0)*x); }
    vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
    vec3 fade(vec3 t) { return t*t*t*(t*(t*6.0-15.0)+10.0); }

    float noise(vec3 P) {
       vec3 i0 = mod289(floor(P)), i1 = mod289(i0 + vec3(1.0));
       vec3 f0 = fract(P), f1 = f0 - vec3(1.0), f = fade(f0);
       vec4 ix = vec4(i0.x, i1.x, i0.x, i1.x), iy = vec4(i0.yy, i1.yy);
       vec4 iz0 = i0.zzzz, iz1 = i1.zzzz;
       vec4 ixy = permute(permute(ix) + iy), ixy0 = permute(ixy + iz0), ixy1 = permute(ixy + iz1);
       vec4 gx0 = ixy0 * (1.0 / 7.0), gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
       vec4 gx1 = ixy1 * (1.0 / 7.0), gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
       gx0 = fract(gx0); gx1 = fract(gx1);
       vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0), sz0 = step(gz0, vec4(0.0));
       vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1), sz1 = step(gz1, vec4(0.0));
       gx0 -= sz0 * (step(0.0, gx0) - 0.5); gy0 -= sz0 * (step(0.0, gy0) - 0.5);
       gx1 -= sz1 * (step(0.0, gx1) - 0.5); gy1 -= sz1 * (step(0.0, gy1) - 0.5);
       vec3 g0 = vec3(gx0.x,gy0.x,gz0.x), g1 = vec3(gx0.y,gy0.y,gz0.y),
            g2 = vec3(gx0.z,gy0.z,gz0.z), g3 = vec3(gx0.w,gy0.w,gz0.w),
            g4 = vec3(gx1.x,gy1.x,gz1.x), g5 = vec3(gx1.y,gy1.y,gz1.y),
            g6 = vec3(gx1.z,gy1.z,gz1.z), g7 = vec3(gx1.w,gy1.w,gz1.w);
       vec4 norm0 = taylorInvSqrt(vec4(dot(g0,g0), dot(g2,g2), dot(g1,g1), dot(g3,g3)));
       vec4 norm1 = taylorInvSqrt(vec4(dot(g4,g4), dot(g6,g6), dot(g5,g5), dot(g7,g7)));
       g0 *= norm0.x; g2 *= norm0.y; g1 *= norm0.z; g3 *= norm0.w;
       g4 *= norm1.x; g6 *= norm1.y; g5 *= norm1.z; g7 *= norm1.w;
       vec4 nz = mix(vec4(dot(g0, vec3(f0.x, f0.y, f0.z)), dot(g1, vec3(f1.x, f0.y, f0.z)),
                          dot(g2, vec3(f0.x, f1.y, f0.z)), dot(g3, vec3(f1.x, f1.y, f0.z))),
                     vec4(dot(g4, vec3(f0.x, f0.y, f1.z)), dot(g5, vec3(f1.x, f0.y, f1.z)),
                          dot(g6, vec3(f0.x, f1.y, f1.z)), dot(g7, vec3(f1.x, f1.y, f1.z))), f.z);
       return 2.2 * mix(mix(nz.x,nz.z,f.y), mix(nz.y,nz.w,f.y), f.x);
    }


    float raySphere(vec3 source, vec3 rayDirection, Sphere sphere)
    {
        vec3 u = source - sphere.location;
        vec3 v = rayDirection;

        float a = 1.0;
        float b = 2.0 * dot(u, v);
        float c = dot(u, u) - pow(sphere.radius, 2.0);
        float delta = b * b - 4.0 * a * c;

        if (delta < 0.0) return INF;

        float root = ((-b) - sqrt(delta)) / (2.0 * a);
        return root < 0.0 ? INF : root;
    }

    bool traceLight(vec3 source, vec3 lightDirection)
    {
        for (int i = 0; i < NUMBER_OF_SPHERES; i++) {
            if (raySphere(source, lightDirection, Spheres[i]) < INF)
                return false;
        }
        return true;
    }

    float phong(vec3 normal, vec3 eyeDirection, vec3 lightDirection)
    {
        vec3 r = 2.0 * dot(normal, lightDirection) * normal - lightDirection;
        return pow(max(0.0, dot(eyeDirection, r)), PHONG_COEF);
    }

    float blinn(vec3 normal, vec3 eyeDirection, vec3 lightDirection)
    {
        vec3 h = normalize(lightDirection + eyeDirection);
        return pow(max(0.0, dot(normal, h)), BLINN_COEF);
    }

    vec3 turbulence(vec3 location, vec3 mixer)
    {
        float t = 0.0, p = 1.0;
        for (int i = 0; i < 5; i++) {
            t += abs(noise(p * location)) / p;
            p *= 2.0;
        }
        t *= 0.8;
        float s = pow(0.5 + 0.5 * cos(7.0 * location.x + 10.0 * t + 0.5 * uTime), 0.1);
        return vec3(s * mixer.r, s * s * mixer.g, s * s * s * mixer.b);
    }

    vec3 shadeReflection(vec3 source, vec3 rayDirection)
    {
        float minimumT = INF;
        vec3 reflectionColor = vec3(0.0, 0.0, 0.0);
        vec3 reflectionLocation, reflectionNormal;
        Material reflectionMaterial;

        for (int i = 0; i < NUMBER_OF_SPHERES; i++) {
            float t = raySphere(source, rayDirection, Spheres[i]);
            if (t < minimumT) {
                minimumT = t;
                reflectionLocation = source + minimumT * rayDirection;
                reflectionNormal = normalize(reflectionLocation - Spheres[i].location);
                reflectionMaterial = Spheres[i].material;
            }
        }

        if (reflectionMaterial.noise) {
            reflectionColor = turbulence(reflectionLocation, reflectionMaterial.ambient);
        } else {
            reflectionColor = reflectionMaterial.ambient;
        }

        if (minimumT < INF) {
            vec3 eyeDirection = normalize(-1.0 * rayDirection);
            for (int i = 0; i < NUMBER_OF_LIGHTS; i++) {
                if (dot(reflectionNormal, Lights[i].direction) >= 0.0 &&
                    traceLight(reflectionLocation, Lights[i].direction)) {
                    reflectionColor += Lights[i].color * reflectionMaterial.diffuse * max(0.0, dot(reflectionNormal, Lights[i].direction));
                    reflectionColor += Lights[i].color * reflectionMaterial.specular *
                        (uPhong ? phong(reflectionNormal, eyeDirection, Lights[i].direction) :
                                  blinn(reflectionNormal, eyeDirection, Lights[i].direction));
                }
            }
        }

        return reflectionColor;
    }

    vec3 SurfaceLocation, SurfaceNormal, EyeDirection;
    Material SurfaceMaterial;

    vec4 shadeSurface()
    {
        vec3 color;

        if (SurfaceMaterial.noise) {
            color = turbulence(SurfaceLocation, SurfaceMaterial.ambient);
            //color = vec3(SurfaceMaterial.ambient.r * f, SurfaceMaterial.ambient.g * f * f, SurfaceMaterial.ambient.b * f * f * f);
        } else {
            color = SurfaceMaterial.ambient;
        }

        for (int i = 0; i < NUMBER_OF_LIGHTS; i++) {
            if (dot(SurfaceNormal, Lights[i].direction) >= 0.0 &&
                traceLight(SurfaceLocation, Lights[i].direction)) {
                color += Lights[i].color * SurfaceMaterial.diffuse * max(0.0, dot(SurfaceNormal, Lights[i].direction));
                color += Lights[i].color * SurfaceMaterial.specular *
                    (uPhong ? phong(SurfaceNormal, EyeDirection, Lights[i].direction) :
                              blinn(SurfaceNormal, EyeDirection, Lights[i].direction));
            }
        }

        vec3 reflectDirection = 2.0 * dot(SurfaceNormal, EyeDirection) * SurfaceNormal - EyeDirection;
        color += SurfaceMaterial.reflectionCoeff * shadeReflection(SurfaceLocation, reflectDirection);

        //Compute Refraction
        if (SurfaceMaterial.refractionCoeff > 0.0) {
            vec3 yAxis = normalize(SurfaceNormal);
            vec3 zAxis = normalize(cross(SurfaceNormal, EyeDirection));
            vec3 xAxis = cross(yAxis, zAxis);

            float sin = SurfaceMaterial.refractionCoeff * sin(acos(dot(EyeDirection, SurfaceNormal)));
            float cos = sqrt(1.0 - pow(sin, 2.0));
            vec3 refractionDirection = sin * xAxis + (-cos) * yAxis;

            color += shadeReflection(SurfaceLocation - 0.02 * SurfaceNormal, refractionDirection);
        }

        return vec4(color, 1.0);
    }

    void castRay()
    {
        vec3 rayDirection = normalize(vPosition - FocusPosition);
        float minimumT = INF;

        for (int i = 0; i < NUMBER_OF_SPHERES; i++) {
            float t = raySphere(FocusPosition, rayDirection, Spheres[i]);
            if (t < minimumT) {
                minimumT = t;
                SurfaceLocation = FocusPosition + minimumT * rayDirection;
                SurfaceNormal = normalize(SurfaceLocation - Spheres[i].location);
                EyeDirection = normalize(FocusPosition - SurfaceLocation);
                SurfaceLocation = SurfaceLocation + 0.01 * SurfaceNormal;
                SurfaceMaterial = Spheres[i].material;
            }
        }

        gl_FragColor = BACKGROUND_COLOR;

        if (minimumT < INF)
            gl_FragColor = shadeSurface();
    }

    void initialize()
    {
        FocusPosition = vec3(0.0, 0.0, FOCAL_LENGTH);

        Lights[0].direction = normalize(vec3(0.0, 0.0, 1.0));
        Lights[0].direction = normalize(vec3(cos(uTime), sin(uTime), 2.0 - 2.0 * cos(uTime) * sin(uTime)));
        Lights[0].color = vec3(1.0, 0.5, 0.0);

        Lights[1].direction = normalize(vec3(0.0, 1.0, 0.0));
        Lights[1].color = vec3(0.5, 0.5, 0.0);

        //Spheres[0].location = vec3(uCursor.x, uCursor.y, 0.0);
        Spheres[0].location = vec3(0.0, 0.0, -1.0);
        Spheres[0].radius = 0.6;
        Spheres[0].material.noise = true;
        Spheres[0].material.ambient = vec3(0.3, 0.3, 0.4);
        Spheres[0].material.diffuse = 0.1 * vec3(1.0, 1.0, 1.0);
        Spheres[0].material.specular = 0.1 * vec3(1.0, 1.0, 1.0);
        Spheres[0].material.reflectionCoeff = 0.8;
        Spheres[0].material.refractionCoeff = -1.0;

        Spheres[1].location = vec3(uCursor.x, uCursor.y, 0.0);
        Spheres[1].location = vec3(0.9 * cos(uTime), 0.0, -1.0 + 0.9 * sin(uTime));
        Spheres[1].radius = 0.3;
        Spheres[1].material.noise = false;
        Spheres[1].material.ambient = vec3(0.6, 0.5, 0.5);
        Spheres[1].material.diffuse = vec3(0.3, 0.3, 0.3);
        Spheres[1].material.specular = vec3(0.4, 0.4, 0.4);
        Spheres[1].material.reflectionCoeff = 0.0;
        Spheres[1].material.refractionCoeff = 1.08;
    }

    void main()
    {
        initialize();
        castRay();
    }

</script>


<script type="text/javascript">
    start_gl('mainCanvas',
        document.getElementById('my_vertex_shader').innerHTML,
        document.getElementById('my_fragment_shader').innerHTML);
</script>
